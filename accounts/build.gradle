plugins {
	id 'com.google.protobuf' version '0.8.18'

	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'org.springframework.boot' version '2.5.6'
	id 'org.liquibase.gradle' version '2.0.4'
	id 'java'
}

group = 'com.example'
version = '1.0-SNAPSHOT'

repositories {
	mavenCentral()
}

dependencies {

	compileOnly(
			'jakarta.annotation:jakarta.annotation-api:1.3.5',

			'org.projectlombok:lombok:1.18.22'
	)

	annotationProcessor(
			'org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.2.Final',
			'javax.annotation:javax.annotation-api:1.3.2',
			'com.querydsl:querydsl-apt:5.0.0:jpa',
			'org.projectlombok:lombok:1.18.22'
	)

	implementation(
			'net.devh:grpc-spring-boot-starter:2.14.0.RELEASE',
			'net.devh:grpc-server-spring-boot-starter:2.14.0.RELEASE',
			'com.google.protobuf:protobuf-java:3.19.1',
			'io.grpc:grpc-protobuf:1.42.1',
			'io.grpc:grpc-stub:1.42.1',

			'org.springframework.boot:spring-boot-starter-validation',
			'org.springframework.boot:spring-boot-starter-data-jpa',
			'org.springframework.boot:spring-boot-starter-web',
			'org.liquibase:liquibase-core:4.6.1',
			'com.querydsl:querydsl-core:5.0.0',
			'com.querydsl:querydsl-jpa:5.0.0',
	)

	liquibaseRuntime(
			'org.springframework.boot:spring-boot-starter-data-jpa',
			'org.liquibase.ext:liquibase-hibernate5:4.5.0',
			'org.liquibase:liquibase-core:4.6.1',
			sourceSets.main.output
	)

	runtimeOnly(
			'org.postgresql:postgresql:42.2.24',
			'com.h2database:h2:1.4.200'
	)

	testImplementation 'org.springframework.boot:spring-boot-starter-test'

}

compileJava {
	options.release = 17
	options.encoding = 'UTF-8'
}

diffChangeLog {
	dependsOn compileJava
}

liquibase {
	activities {
		main {
			changeLogFile 'src/main/resources/db/changelog/changelog-master.xml'
			url 'jdbc:h2:./accountsDataBase'
			username ''
			password ''
			referenceUrl 'hibernate:spring:com.example.model.' +
					'?dialect=org.hibernate.dialect.H2Dialect' +
					'&hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy'
		}
	}
}

test {
	useJUnitPlatform()
}

sourceSets.main.java.srcDirs = ['src/main/java', 'build/generated/sources/annotationProcessor/java/main']

sourceSets {
	src {
		main {
			java {
				srcDirs 'build/generated/source/proto/main/grpc'
				srcDirs 'build/generated/source/proto/main/java'
			}
		}
	}
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:3.19.1"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:1.42.1"
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {}
		}
	}
}